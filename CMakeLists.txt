# Orthanc - A Lightweight, RESTful DICOM Store
# Copyright (C) 2012-2015 Sebastien Jodogne, Medical Physics
# Department, University Hospital of Liege, Belgium
#
# This program is free software: you can redistribute it and/or
# modify it under the terms of the GNU Affero General Public License
# as published by the Free Software Foundation, either version 3 of
# the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Affero General Public License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this program. If not, see <http://www.gnu.org/licenses/>.


cmake_minimum_required(VERSION 2.8)

project(OrthancPostgreSQL)

# Parameters of the build
set(STATIC_BUILD OFF CACHE BOOL "Static build of the third-party libraries (necessary for Windows)")
set(ALLOW_DOWNLOADS OFF CACHE BOOL "Allow CMake to download packages")

# Advanced parameters to fine-tune linking against system libraries
set(USE_SYSTEM_JSONCPP ON CACHE BOOL "Use the system version of JsonCpp")
set(USE_SYSTEM_BOOST ON CACHE BOOL "Use the system version of Boost")
set(USE_SYSTEM_GOOGLE_TEST ON CACHE BOOL "Use the system version of Google Test")
set(USE_SYSTEM_LIBPQ ON CACHE BOOL "Use the system version of the PostgreSQL client library")

# Distribution-specific settings
set(USE_GTEST_DEBIAN_SOURCE_PACKAGE OFF CACHE BOOL "Use the sources of Google Test shipped with libgtest-dev (Debian only)")
mark_as_advanced(USE_GTEST_DEBIAN_SOURCE_PACKAGE)

# Force static build when cross-compiling
if (CMAKE_CROSSCOMPILING)
  set(STATIC_BUILD ON)
endif()

if (${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
  link_libraries(uuid)
  SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -pthread")
  SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -pthread")
elseif (${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
  link_libraries(rpcrt4 ws2_32 secur32)
  if (CMAKE_COMPILER_IS_GNUCXX)
    SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -static-libgcc -static-libstdc++")
  endif()
endif ()

if (CMAKE_COMPILER_IS_GNUCXX)
  SET(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} -Wl,--version-script=${CMAKE_SOURCE_DIR}/Resources/VersionScript.map -Wl,--no-undefined")
endif()


include(CheckIncludeFiles)
include(CheckIncludeFileCXX)
include(CheckLibraryExists)
include(${CMAKE_SOURCE_DIR}/Resources/CMake/AutoGeneratedCode.cmake)
include(${CMAKE_SOURCE_DIR}/Resources/CMake/DownloadPackage.cmake)
include(${CMAKE_SOURCE_DIR}/Resources/CMake/BoostConfiguration.cmake)
include(${CMAKE_SOURCE_DIR}/Resources/CMake/PostgreSQLConfiguration.cmake)
include(${CMAKE_SOURCE_DIR}/Resources/CMake/JsonCppConfiguration.cmake)
include(${CMAKE_SOURCE_DIR}/Resources/CMake/GoogleTestConfiguration.cmake)


# Check that the Orthanc SDK headers are available or download them
if (STATIC_BUILD)
  set(ORTHANC_SDK_URL "http://orthanc.googlecode.com/hg") # TODO use 0.8.6 release
  file(MAKE_DIRECTORY ${AUTOGENERATED_DIR}/orthanc)
  file(DOWNLOAD "${ORTHANC_SDK_URL}/Plugins/Include/OrthancCPlugin.h"
    "${AUTOGENERATED_DIR}/orthanc/OrthancCPlugin.h" SHOW_PROGRESS)
  file(DOWNLOAD "${ORTHANC_SDK_URL}/Plugins/Include/OrthancCDatabasePlugin.h"
    "${AUTOGENERATED_DIR}/orthanc/OrthancCDatabasePlugin.h" SHOW_PROGRESS)
  file(DOWNLOAD "${ORTHANC_SDK_URL}/Plugins/Include/OrthancCppDatabasePlugin.h" 
    "${AUTOGENERATED_DIR}/orthanc/OrthancCppDatabasePlugin.h" SHOW_PROGRESS)
  if (${MSVC})
    add_definitions(-D_CRT_SECURE_NO_WARNINGS=1)
    file(DOWNLOAD "${ORTHANC_SDK_URL}/Resources/ThirdParty/VisualStudio/stdint.h" 
      "${AUTOGENERATED_DIR}/stdint.h" SHOW_PROGRESS)
  endif()
else ()
  CHECK_INCLUDE_FILE_CXX(orthanc/OrthancCppDatabasePlugin.h HAVE_ORTHANC_H)
  if (NOT HAVE_ORTHANC_H)
    message(FATAL_ERROR "Please install the headers of the Orthanc plugins SDK")
  endif()
endif()


# Embed the SQL files into the binaries
EmbedResources(
  POSTGRESQL_PREPARE ${CMAKE_CURRENT_SOURCE_DIR}/IndexPlugin/PostgreSQLPrepare.sql
  )

set(CORE_SOURCES
  ${CMAKE_SOURCE_DIR}/Core/PostgreSQLConnection.cpp
  ${CMAKE_SOURCE_DIR}/Core/PostgreSQLLargeObject.cpp
  ${CMAKE_SOURCE_DIR}/Core/PostgreSQLResult.cpp
  ${CMAKE_SOURCE_DIR}/Core/PostgreSQLStatement.cpp
  ${CMAKE_SOURCE_DIR}/Core/PostgreSQLTransaction.cpp
  ${CMAKE_SOURCE_DIR}/Core/Configuration.cpp
  ${CMAKE_SOURCE_DIR}/Core/GlobalProperties.cpp

  ${LIBPQ_SOURCES}
  ${BOOST_SOURCES}
  ${JSONCPP_SOURCES}
  )

add_library(OrthancPostgreSQLStorage
  SHARED
  ${CORE_SOURCES}
  ${CMAKE_SOURCE_DIR}/StoragePlugin/PostgreSQLStorageArea.cpp
  ${CMAKE_SOURCE_DIR}/StoragePlugin/Plugin.cpp
  )

add_library(OrthancPostgreSQLIndex
  SHARED
  ${CORE_SOURCES}
  ${AUTOGENERATED_SOURCES}
  ${CMAKE_SOURCE_DIR}/IndexPlugin/PostgreSQLWrapper.cpp
  ${CMAKE_SOURCE_DIR}/IndexPlugin/Plugin.cpp
  )

add_executable(UnitTests
  ${CORE_SOURCES}
  ${GTEST_SOURCES}
  ${AUTOGENERATED_SOURCES}
  ${CMAKE_SOURCE_DIR}/IndexPlugin/PostgreSQLWrapper.cpp
  ${CMAKE_SOURCE_DIR}/StoragePlugin/PostgreSQLStorageArea.cpp
  ${CMAKE_SOURCE_DIR}/UnitTestsSources/UnitTestsMain.cpp
  ${CMAKE_SOURCE_DIR}/UnitTestsSources/PostgreSQLTests.cpp
  ${CMAKE_SOURCE_DIR}/UnitTestsSources/PostgreSQLWrapperTests.cpp
  )
